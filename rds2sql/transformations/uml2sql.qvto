import common;

modeltype uml uses uml('http://www.eclipse.org/uml2/4.0.0/UML');
modeltype sql uses genericsql('http://genericsql/1.0');

transformation mysql(in umlModel : uml, out sqlModel : sql);

main() 
{
	umlModel.objects()![Model].map toDataBase();
}

mapping Package::toDataBase() : DataBase
{
	name := self.name;
	tables += self.allSubobjectsOfKind(Class)[Class]->map toTable();
	umlModel.objects()[Association]->map toForeignKeys();
	umlModel.objects()[Association]->map toTable();
}

mapping Class::toTable() : Table
{
	name := self.name;
	primaryKey := self.ownedAttribute->at(1).map toPrimaryKey(self.ownedAttribute->at(1).name);
	self.ownedAttribute->forEach(att) {
		fields += att->map toField(att.name);
	}
}

mapping Property::toField(s :String) : Field
disjuncts Property::toSizeField, Property::toGeneralField
{}

abstract mapping Property::toCommonField(s :String) : Field
{
	name := s;
	type := self.translate();
	notNull := self.lower > 0;
	unique := self.isUnique;
}

mapping Property::toGeneralField(s :String) : Field
inherits Property::toCommonField
{
}

mapping Property::toSizeField(s :String) : Field
inherits Property::toCommonField
when {
	self.type.name = 'String' or self.type.name = 'ByteArray' or self.type.name = 'Char'
}
{
	if (self.type.name = 'Char')
		then { size := 1; }
	else
		{ size := 100; }
	endif;
}

mapping Property::toPrimaryKey(s : String) : PrimaryKey
{
	name := self._class.name;
	primaryFields := self.map toField(s);
}

mapping Association::toPrimaryKey(t : Table) : PrimaryKey
when
{
	(self.ownedEnd->at(1).upperBound() < 0 and self.ownedEnd->at(1).getOtherEnd().upperBound() < 0)
}
{
	var propRef := self.ownedEnd->at(1);
	var pk1 := propRef.type![Class]->ownedAttribute->at(1);
	var pk2 := propRef.getOtherEnd().type![Class]->ownedAttribute->at(1);
	
	name := t.name;
	table := t;
	primaryFields += pk1.map toField(propRef.name+'_'+pk1.name);
	primaryFields += pk2.map toField(propRef.getOtherEnd().name+'_'+pk2.name);
}

mapping Association::toForeignKeys() : ForeignKey
disjuncts
	Association::toForeignKeysOne2One,
	Association::toForeignKeysOne2N
{}

mapping Association::toForeignKeysOne2One() : ForeignKey
when
{
	self.ownedEnd->at(1).upperBound() = 1
	and
	self.ownedEnd->at(1).getOtherEnd().upperBound() = 1
}
{
	var propRef := 
		if(self.ownedEnd->at(1).lowerBound() = 0) then 
			self.ownedEnd->at(2)
		else
			self.ownedEnd->at(1)
		endif;
	
	var attribute := propRef.type![Class]->ownedAttribute->at(1);
	refersTo :=  attribute.map toPrimaryKey(attribute.name);
	table :=  propRef.getOtherEnd().type![Class].map toTable();
	foreignFields += propRef.map toForeignField(table,propRef.name+'_'+attribute.name);
	name := propRef.name;
}

mapping Association::toForeignKeysOne2N() : ForeignKey
when
{
	(self.ownedEnd->at(1).upperBound() = 1 and self.ownedEnd->at(1).getOtherEnd().upperBound() < 0)
	or
	(self.ownedEnd->at(1).upperBound() < 0  and self.ownedEnd->at(1).getOtherEnd().upperBound() = 1)
}
{	
	var propRef := 
		if(self.ownedEnd->at(1).upperBound() < 0) then 
			self.ownedEnd->at(2)
		else
			self.ownedEnd->at(1)
		endif;
	
	var attribute := propRef.type![Class]->ownedAttribute->at(1);
	refersTo :=  attribute.map toPrimaryKey(attribute.name);
	table :=  propRef.getOtherEnd().type![Class].map toTable();
	foreignFields += propRef.map toForeignField(table,propRef.name+'_'+attribute.name);
	name := propRef.name;
}

mapping Association::toForeignKeysN2N(t : Table)
when
{
	(self.ownedEnd->at(1).upperBound() < 0 and self.ownedEnd->at(1).getOtherEnd().upperBound() < 0)
}
{	
	var propRef := self.ownedEnd->at(1);	
	object ForeignKey
	{
		var attribute := propRef.type![Class]->ownedAttribute->at(1);
		refersTo :=  attribute.map toPrimaryKey(attribute.name);
		table :=  t;
		foreignFields := propRef.map toForeignField(t,propRef.name+'_'+attribute.name);
		name := propRef.name;
	};	
	object ForeignKey
	{
		var attribute := propRef.getOtherEnd().type![Class]->ownedAttribute->at(1);
		refersTo :=  attribute.map toPrimaryKey(attribute.name);
		table :=  t;
		foreignFields := propRef.getOtherEnd().map toForeignField(t,propRef.getOtherEnd().name+'_'+attribute.name);
		name := propRef.getOtherEnd().name;
	}; 
}

mapping Association::toTable() : Table
when
{
	(self.ownedEnd->at(1).upperBound() < 0 and self.ownedEnd->at(1).getOtherEnd().upperBound() < 0)
}
{
	var propRef := self.ownedEnd->at(1);
	
	database := umlModel.objects()![Model].map toDataBase();
	name := propRef.type![Class].name + '_' + propRef.getOtherEnd().type![Class].name;
	self.map toPrimaryKey(result);
			  
	foreignKeys += self.map toForeignKeysN2N(result);
}


abstract mapping Property::toCommonForeignField(t : Table, s : String) : Field
{
	var pk := self.type![Class]->ownedAttribute->at(1).map toPrimaryKey(s);
	name := pk.primaryFields->at(1).name;
	type := pk.primaryFields->at(1).type;		
}

mapping Property::toForeignField(t : Table, s : String) : Field
inherits Property::toCommonForeignField
{
	if (self.owner![Association].ownedEnd->at(1).upperBound() = 1
		and self.owner![Association].ownedEnd->at(1).getOtherEnd().upperBound() = 1)
		then { notNull := true; unique := true; }
	else
		if (self.owner![Association].ownedEnd->at(1).upperBound() < 0
			and self.owner![Association].ownedEnd->at(1).getOtherEnd().upperBound() < 0)
			then {}
		else
			{ notNull := true; }
		endif
	endif;
	table := t;
}

property typeMap : Dict(String,sql::Type) = 
	Dict {
        'Integer'			= sql::Type::int,
        'Boolean'			= sql::Type::boolean,
        'ByteArray'			= sql::Type::byteArray,
        'Date'				= sql::Type::date,
        'Long'				= sql::Type::bigInt,
        'UnlimitedNatural'	= sql::Type::bigInt,
        'Real'				= sql::Type::double,
        'String'			= sql::Type::varchar,
        'Char'				= sql::Type::varchar
    };

query TypedElement::translate() : sql::Type
{
	var res := typeMap->get(self.type.name);
	return if (res <> null) then res else sql::Type::undefined endif;
}